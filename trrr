#!/usr/bin/env perl

use strict;
use warnings;

=head1 NAME

trrr - search torrents 

=cut

use Term::ANSIColor;
use App::Trrr qw< open_app >;
use App::Trrr::KAT qw< kat >;
use App::Trrr::TPB qw< tpb >;
use App::Trrr::RBG qw< rbg >;
use App::Trrr::YTS qw< yts >;
use App::Trrr::EXT qw< ext >;
use App::Trrr::X137 qw< x137 >;
use App::Trrr::LME qw< lme >;
use App::Trrr::Clipboard qw< clip >;


my @keyword = ();
if( scalar @ARGV == 0 or (scalar @ARGV == 1 and $ARGV[0] =~ /^-(P|R|Y|K|E|X|L)$/) ){
    push @keyword, split(/\s+/, clip());
}


my $opt = {
    seeds   =>  1,
    api     =>  "tpb"
};

for(@ARGV){
    if(/^\-/){
	s/\-//;
        if(/^(-help|help)$/){ help() and exit }
        if(/^[a-o]$/){ $opt->{key} = $_ }
        if(/^\d+$/){ $opt->{seeds} = $_ } 
        if(/^P$/){ $opt->{api} = "tpb" }
        if(/^R$/){ $opt->{api} = "rbg" }
        if(/^Y$/){ $opt->{api} = "yts" }
        if(/^K$/){ $opt->{api} = "kat" }
        if(/^E$/){ $opt->{api} = "ext" }
        if(/^X$/){ $opt->{api} = "x137" }
        if(/^L$/){ $opt->{api} = "lme" }
    } else { push @keyword, $_ }
}


sub strip_title {
    my $item = shift;
    
    my $term_width = 0; 
    open(my $tput_ph, '-|', 'tput', 'cols');
    while(<$tput_ph>){
	    chomp;
	    $term_width = $_;
    }
    close $tput_ph;

    my $line = $item->{key} . ' ' . $item->{seeds} . ' ' . "$item->{title}" . ' ' . $item->{category} . ' ' . $item->{size};
    if( $term_width < length($line)){
        my $strip = length($line) - $term_width;
	my $max = length($item->{title}) - $strip - 3;
        $item->{title} = substr($item->{title}, 0, $max);
	$item->{title} = $item->{title} . '...';
    } 
    return $item->{title};
}


sub show {
    no strict "refs";
    my $key = 'A';
    my( $key_color ) = ();
    my $i = 1;

    my @result = grep { int($_->{seeds}) >= int($opt->{seeds}) } @{ $opt->{api}(\@keyword) };
    @result = sort { $b->{seeds} <=> $a->{seeds} } @result;
    @result = splice(@result,0,15);

    unless(@result){ print colored(['yellow'], 'no results') . "\n" and exit }
    
    for(@result){
        if( $i % 2 ){ $key_color = 'black on_white' } else { $key_color = 'white on_black' }
        $_->{key} = $key;

        if($_->{api} eq 'yts'){
		print colored([$key_color], $key) . ' ' .  colored( ['yellow'], strip_title($_) ) . ' ' . colored(['yellow'], $_->{year}) . ' ' . colored(['grey8'], $_->{category}) . "\n";
	} else {
		print colored([$key_color], $key) . ' ' . colored(['cyan'], $_->{seeds}) . ' ' .  colored( ['yellow'], strip_title($_) ) . ' ' . colored(['grey8'], $_->{category}) . ' ' . colored(['bold'], $_->{size}) . "\n";
	}
        $key++; $i++;
    }
    wait_key(\@result);
}

sub wait_key {
    my $result = shift;

    if( $opt->{key} ){
	get_torrent($result) and exit;
    } else {

        if( $^O eq 'MSWin32' or $^O eq 'msys' ){ 
            print "To pick from results repeat search and add -[key] (e.g. -a)\n" and exit;
        }
	
        print colored(['blink'],'^') . ' ' . colored(['grey5 on_grey15'],'P') . colored(['grey15 on_grey5'],'RESS') . colored(['grey5 on_grey15'],'K') . colored(['grey15 on_grey5'],'EY') . "\n";
        require App::Trrr::HotKey;
        App::Trrr::HotKey->import( 'readkey' ) ;
        $opt->{key} = readkey();
        get_torrent($result);
    }
}

sub get_torrent {
    no strict "refs";
    my $result = shift;
    my( $picked ) = grep { $_->{key} eq uc $opt->{key} } @$result;

    if( $picked->{magnet} ){
	open_app("$picked->{magnet}") if $opt->{key} =~ /[a-o]/;
    } else { 
        open_app( $picked->{api}($picked->{link}) ) if $opt->{key} =~ /[a-o]/;
    }
}


sub help {
    my $help = <<'END_HELP';
trrr - search torrents

E.g:
       "trrr keyword1 keyword2" search for keywords on piratebay
       "trrr keyword1 keyword2 -R" search for keywords on rarbg 
Options: 
       "-R" rarbg
       "-Y" yts
       "-K" kickasstorrents
       "-E" extratorrents
       "-X" 1337x
       "-L" limetorrents
       
       "-100" limit results to those that have as least 100 seeders
       "-b" automaticaly open second (B) magnet link from results
       "perldoc trrr" read more
END_HELP

    print "$help\n";
}


show();


=head1 DESCRIPTION
    
Tool for searching torrents. Results are sorted by number of seeders and each is mapped to keyboard key. Pressing the key will open magnet link in your default torrent client. On iOS magnet link is placed into clipboard instead.

=head1 USAGE
    
- Search with as many keywords as needed. Uses piratebay by default.

=over 10

C<trrr keyword1 keyword2 keywordN>

=back

- To use different torrent source add one of the following options. 

=over 10

C<-R> rarbg

C<-Y> yts

C<-K> kickasstorrents

C<-E> extratorrents

C<-X> 1337x

C<-L> limetorrents

=back

- On Linux, start it without any parameter and it'll use clipboard content as keywords. ( needs 'xclip' or 'xsel' to be installed )

=over 10

C<trrr>

=back

- Limit results which have at least 100 seeders:

=over 10

C<trrr keyword1 keyword2 keywordN -100>

=back

- To automaticaly open some magnet link from results add its key -[a-o] as an option. So to open first magnet link use following command.

=over 10

C<trrr keyword1 keyword2 keywordN -a>

=back

On Windows running 'Git/Bash for Windows' you have to specify key upfront so first make search without any option to see the results, then repeat the command and add key -[a-o] as anoption. 

- See help

=over 10

C<trrr -help>

=back

=head1 AUTHOR

Zdenek Bohunek. <zdenek@cpan.org>

App::Trr::HotKey is taken from StackOverflow post by brian d foy

=head1 COPYRIGHT AND LICENSE

Copyright 2016 by Zdenek Bohunek

This library is free software; you can redistribute it and/or modify it under the same terms as Perl itself.

=cut
